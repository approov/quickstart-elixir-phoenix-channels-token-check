# Approov Token Binding Quickstart

This quickstart is for developers familiar with Phoenix who are looking for a quick intro into how they can add [Approov](https://approov.io) into an existing project. Therefore this will guide you through the necessary steps for adding Approov to an existing Elixir Phoenix Channels server.

## TOC - Table of Contents

* [Why?](#why)
* [How it Works?](#how-it-works)
* [Requirements](#requirements)
* [Approov Setup](#approov-setup)
* [Approov Token Check](#approov-token-binding-check)
* [Test the Approov Integration](#test-the-approov-integration)


## Why?

To lock down your Elixir Phoenix Channels server to your mobile app. Please read the brief summary in the [README](/README.md#why) at the root of this repo or visit our [website](https://approov.io/product) for more details.

[TOC](#toc---table-of-contents)


## How it works?

For more background on Approov, see the overview in the [README](/README.md#how-it-works) at the root of this repo.

### Approov Token Check

Take a look at the [Approov Token Plug](/src/approov-protected-server/token-binding-check/echo/lib/echo_web/plugs/approov_token_plug.ex) module to see how the Approov token check is invoked in the `call/2` function. To see the simple code for the Approov token check, you need to look into the `verify/1` function in the [Approov Token](/src/approov-protected-server/token-binding-check/echo/lib/approov_token.ex) module.

### Approov Token Binding Check

The Approov token binding check can only be performed after a successful Approov token check, because it uses the `pay` key from the claims of the decoded Approov token payload. Take a look at the [Approov Token Binding Plug](/src/approov-protected-server/token-binding-check/echo/lib/echo_web/plugs/approov_token_binding_plug.ex) module to see how the Approov token binding check is invoked in the `call/2` function. To see the simple code for the Approov token binding check you need to look into the `verify_token_binding/1` function in the [Approov Token](/src/approov-protected-server/token-binding-check/echo/lib/approov_token.ex) module.

[TOC](#toc---table-of-contents)


## Requirements

To complete this quickstart you will need both the Phoenix and the Approov CLI tool installed.

* Phoenix - Follow the official installation instructions from [here](https://hexdocs.pm/phoenix/installation.html#content)
* Approov CLI - Follow our [installation instructions](https://approov.io/docs/latest/approov-installation/#approov-tool) and read more about each command and its options in the [documentation reference](https://approov.io/docs/latest/approov-cli-tool-reference/)

[TOC](#toc---table-of-contents)


## Approov Setup

To use Approov with the Elixir Phoenix Channels server you need a small amount of configuration. First, Approov needs to know the API domain that will be protected. Second, the Elixir Phoenix Channels server needs the Approov Base64 encoded secret that will be used to verify the tokens generated by the Approov cloud service.

### Configure API Domain

Approov needs to know the domain name of the API for which it will issue tokens.

Add it with:

```text
approov api -add your.api.domain.com
```

Adding the API domain also configures the [dynamic certificate pinning](https://approov.io/docs/latest/approov-usage-documentation/#approov-dynamic-pinning) setup, out of the box.

> **NOTE:** By default the pin is extracted from the public key of the leaf certificate served by the domain, as visible to the box executing the Approov CLI command and the Approov servers.

### Approov Secret

Approov tokens are signed with a symmetric secret. To verify tokens, you need to grab the secret using the [Approov secret command](https://approov.io/docs/latest/approov-cli-tool-reference/#secret-command) and plug it into the Elixir Phoenix Channels server environment to check the signatures of the [Approov Tokens](https://www.approov.io/docs/latest/approov-usage-documentation/#approov-tokens) that it processes.

Retrieve the Approov secret with:

```text
approov secret -get base64url
```

> **NOTE:** The `approov secret` command requires an [administration role](https://approov.io/docs/latest/approov-usage-documentation/#account-access-roles) to execute successfully.

#### Set the Approov Secret

Export the Approov secret into the environment:

```text
export APPROOV_BASE64URL_SECRET=approov_base64url_secret_here
```

For the Approov secret to be available during runtime you need to add some code to the Elixir configuration, but Elixir has compile time and runtime configuration. To not duplicate the Approov secret configuration we recommend you to add it only in the runtime configuration.

##### From Elixir 1.11

If doesn't exist already, create the file `config/runtime.exs` and then add:

```elixir
import Config

approov_secret =
  System.get_env("APPROOV_BASE64URL_SECRET") ||
    raise "Environment variable APPROOV_BASE64URL_SECRET is missing."

config :YOUR_APP, ApproovToken,
  secret_key: approov_secret
```

The runtime configuration will run every-time a release or a Mix project is started.

##### From Elixir 1.9 to 1.10

The runtime configuration in this versions is `config/releases.exs`, but with the difference that it only runs when a release is booted, not when a Mix project is started, where only the compile time configuration is used.

To avoid duplicating the code for reading the Approov secret in both configurations you may want to import the runtime configuration from within your compile time configuration.

Add to `config/releases.exs`:

```elixir
import Config

approov_secret =
  System.get_env("APPROOV_BASE64URL_SECRET") ||
    raise "Environment variable APPROOV_BASE64URL_SECRET is missing."

config :YOUR_APP, ApproovToken,
  secret_key: approov_secret

config :YOUR_APP, YOUR_APP.Endpoint, server: true
```

Then import this file from `config/config.exs`:

```elixir
import_config "releases.exs"

# Import environment specific config. This must remain at the bottom
# of this file so it overrides the configuration defined above.
import_config "#{Mix.env()}.exs"
```

With this approach you are in fact emulating the Elixir `1.11` behaviour for the runtime configuration `config/runtime.exs`.

##### Until Elixir 1.8

Until Elixir `1.8` version no official way existed of reading values for each time the server was started from a release or a Mix project, and lots of different workarounds existed, and you can see some of them being discussed in the Elixir Forum, like in this [topic](https://elixirforum.com/t/phoenix-distillery-kubernetes-and-runtime-env-variables/13399).

[TOC](#toc---table-of-contents)


## Approov Token Check

For protecting HTTP requests and/or Websocket requests you need to add the Approov token module to perform the token check, that will use the [potatosalad/erlang-jose](https://github.com/potatosalad/erlang-jose) package to verify the token signature, and a custom function to check the expiration time for it.

Add the [Approov Token](/src/approov-protected-server/token-binding-check/echo/lib/approov_token.ex) module into your project at `lib/approov_token.ex`:

```elixir
defmodule ApproovToken do
  require Logger

  require Plug.Conn

  def verify(%Plug.Conn{} = conn, %{} = approov_jwk) do
    with {:ok, approov_token} <- _get_approov_token_header(conn),
         {:ok, approov_token_claims} <- _verify_approov_token(approov_token, approov_jwk) do
      {:ok, approov_token_claims}
    else
      {:error, reason} ->
        {:error, reason}
    end
  end

  def verify(%{} = params, %{} = approov_jwk) do
    with {:ok, approov_token} <- _get_approov_token(params),
         {:ok, approov_token_claims} <- _verify_approov_token(approov_token, approov_jwk) do

      {:ok, approov_token_claims}
    else
      {:error, reason} ->
        {:error, reason}
    end
  end

  defp _get_approov_token_header(conn) do
    case Plug.Conn.get_req_header(conn, "x-approov-token") do
      [] ->
        Logger.info("Approov token not in the headers. Next, try to retrieve from url query params.")
        _get_approov_token(conn.params)

      [approov_token | _] ->

        {:ok, approov_token}
    end
  end

  defp _get_approov_token(%{x_headers: x_headers})
    when is_list(x_headers) and length(x_headers) > 0
  do
    case Utils.filter_list_of_tuples(x_headers, "x-approov-token") do
      nil ->
        {:ok, Utils.filter_list_of_tuples(x_headers, "X-Approov-Token")}

      approov_token ->
        {:ok, approov_token}
    end
  end

  defp _get_approov_token(%{"x-approov-token" => approov_token}), do: {:ok, approov_token}
  defp _get_approov_token(%{"X-Approov-Token" => approov_token}), do: {:ok, approov_token}
  defp _get_approov_token(_params), do: {:error, :missing_approov_token}

  defp _verify_approov_token(approov_token, approov_jwk) do
    with {:ok, approov_token_claims} <- _decode_and_verify(approov_token, approov_jwk),
         true <- _has_expiration_claim?(approov_token_claims),
         :ok <- _verify_expiration(approov_token_claims) do
      {:ok, approov_token_claims}
    else
      {:error, reason} ->
        {:error, reason}

      false ->
        {:error, :approov_token_missing_expiration_claim}
    end
  end

  defp _decode_and_verify(approov_token, approov_jwk)
    when is_binary(approov_token) and byte_size(approov_token) > 0
  do
    case JOSE.JWT.verify_strict(approov_jwk, ["HS256"], approov_token) do
      {true, approov_token_claims, _jws} ->
        {:ok, approov_token_claims}

      {false, _approov_token_claims, _jws} ->
        {:error, :approov_token_invalid_signature}

      {:error, {:badarg, _arg}} ->
        {:error, :approov_token_malformed}

      {:error, _reason} ->
        {:error, :jwt_library_internal_error}
    end
  end

  defp _decode_and_verify(_approov_token, _approov_jwk) do
    {:error, :approov_token_empty}
  end

  defp _has_expiration_claim?(%JOSE.JWT{fields: %{"exp" => _exp}}), do: true
  defp _has_expiration_claim?(_approov_token_claims), do: false

  defp _verify_expiration(%JOSE.JWT{fields: %{"exp" => timestamp}}) do
    datetime = _timestamp_to_datetime(timestamp)
    now = DateTime.utc_now()

    case DateTime.compare(now, datetime) do
      :lt ->
        :ok

      _ ->
        {:error, :approov_token_expired}
    end
  end

  defp _timestamp_to_datetime(timestamp) when is_integer(timestamp) do
    DateTime.from_unix!(timestamp)
  end

  defp _timestamp_to_datetime(timestamp) when is_float(timestamp) do
    # iex> Integer.parse "1555083349.3777623"
    # {1555083349, ".3777623"}
    {timestamp, _decimals} = Integer.parse("#{timestamp}")
    DateTime.from_unix!(timestamp)
  end

  def verify_token_binding(%Plug.Conn{private: %{approov_token_claims: approov_token_claims}} = conn) do
    with {:ok, token_binding_header} <- _get_token_binding_header(conn),
         :ok <- _verify_approov_token_binding(approov_token_claims, token_binding_header)
    do
      :ok
    else
      {:error, reason} ->
        {:error, reason}
    end
  end

  def verify_token_binding(approov_token_claims, %{} = params) do
    with {:ok, token_binding_header} <- _get_token_binding(params),
         :ok <- _verify_approov_token_binding(approov_token_claims, token_binding_header)
    do
      :ok
    else
      {:error, reason} ->
        {:error, reason}
    end
  end

  defp _get_token_binding_header(%Plug.Conn{} = conn) do
    # We use the Authorization token, but feel free to use another header in
    # the request. Bear in mind that it needs to be the same header used in the
    # mobile app to bind the request with the Approov token.
    case Plug.Conn.get_req_header(conn, "authorization") do
      [] ->
        Logger.info("Approov token binding header is missing. Next, try to retrieve from the url query params.")
        _get_token_binding(conn.params)

      [token_binding_header | _] ->

        {:ok, token_binding_header}
    end
  end

  defp _get_token_binding(%{"Authorization" => token}) when is_binary(token), do: {:ok, token}
  defp _get_token_binding(_params), do: {:error, :missing_approov_token_binding}

  defp _verify_approov_token_binding(
          %JOSE.JWT{fields: %{"pay" => token_binding_claim}} = _approov_token_claims,
         token_binding_header
       )
  do
    # We need to hash and base64 encode the token binding header, because that's
    # how it was included in the Approov token on the mobile app.
    token_binding_header_encoded =
      :crypto.hash(:sha256, token_binding_header)
      |> Base.encode64()

    case token_binding_claim === token_binding_header_encoded do
      true ->
        :ok

      false ->
        {:error, :approov_invalid_token_binding_header}
    end
  end

  defp _verify_approov_token_binding(_approov_token_claims, _token_binding_header) do
    {:error, :approov_token_missing_pay_claim}
  end
end
```

### Approov Token Check for HTTP Requests

First, add the [Approov Token Plug](/src/approov-protected-server/token-binding-check/echo/lib/echo_web/plugs/approov_token_plug.ex) module to your project at `lib/your_app_web/plugs/approov_token_plug.ex`:

```elixir
defmodule YourAppWeb.ApproovTokenPlug do
  require Logger

  ##############################################################################
  # Adhere to the Phoenix Module Plugs specification by implementing:
  #   * init/1
  #   * call/2
  #
  # @link https://hexdocs.pm/phoenix/plug.html#module-plugs
  ##############################################################################

  # Don't use this function to init the Plug with the Approov secret, because
  # this is only evaluated at compile time, and we don't want the to have
  # secrets inside a release. Secrets must always be retrieved from the
  # environment where the release is running.
  def init(opts), do: opts

  def call(conn, _opts) do

    # Check the `init/1` comment to see why we don't do it there.
    approov_jwk = %{
      "kty" => "oct",
      "k" =>  Application.fetch_env!(:your_app, ApproovToken)[secret_key]
    }

    with {:ok, approov_token_claims} <- ApproovToken.verify(conn, approov_jwk) do
      conn
      |> Plug.Conn.put_private(:approov_token_claims, approov_token_claims)
    else
      {:error, reason} ->
        _log_error(reason)

        conn
        |> _halt_connection()
    end
  end

  defp _log_error(reason) when is_atom(reason), do: Logger.warn(Atom.to_string(reason))
  defp _log_error(reason), do: Logger.warn(reason)

  defp _halt_connection(conn) do
    conn
    |> Plug.Conn.put_status(401)
    |> Phoenix.Controller.json(%{})
    |> Plug.Conn.halt()
  end
end

```

> **NOTE:** When the Approov token validation fails we return a `401` with an empty body, because we don't want to give clues to an attacker about the reason the request failed, and you can go even further by returning a `400`.

Next, add the [Approov Token Binding Plug](/src/approov-protected-server/token-binding-check/echo/lib/echo_web/plugs/approov_token_binding_plug.ex) module to your project at `lib/your_app_web/plugs/approov_token_binding_plug.ex`:

```elixir
defmodule YourAppWeb.ApproovTokenBindingPlug do
  require Logger

  ##############################################################################
  # Adhere to the Phoenix Module Plugs specification by implementing:
  #   * init/1
  #   * call/2
  #
  # @link https://hexdocs.pm/phoenix/plug.html#module-plugs
  ##############################################################################

  def init(opts), do: opts

  def call(conn, _opts) do
    with :ok <- ApproovToken.verify_token_binding(conn) do
      conn
    else
      {:error, reason} ->
        _log_error(reason)

        conn
        |> _halt_connection()
    end
  end

  defp _log_error(reason) when is_atom(reason), do: Logger.warn(Atom.to_string(reason))
  defp _log_error(reason), do: Logger.warn(reason)

  defp _halt_connection(conn) do
    conn
    |> Plug.Conn.put_status(401)
    |> Phoenix.Controller.json(%{})
    |> Plug.Conn.halt()
  end
end

```

Now, create and use the pipelines for the Approov token checks at `lib/your_app_web/router.ex`:

```elixir
pipeline :approov_token do
  # Ideally you will not want to add any other Plug before the Approov Token
  # check to protect your server from wasting resources in processing requests
  # not having a valid Approov token. This increases availability for your
  # users during peak time or in the event of a DoS attack(We all know the
  # BEAM design allows to cope very well with this scenarios, but best to play
  # in the safe side).
  plug YourAppWeb.ApproovTokenPlug
end

scope "/" do
  pipe_through :api

  # Your Approov toekn protected endpoints go after this line, for example:
  pipe_through :approov_token
  post "/auth/register", YourAppWeb.AuthController, :register
  post "/auth/login", YourAppWeb.AuthController, :login

  # Your Approov Token Binding protected endpoints go after this line.
  # So, any enpoint here will be checked for a valid Approov token and that the
  # token binding in the `pay` claim matches the token binding header.
  # For example:
  pipe_through :approov_token_binding
  get "/users", YourAppWeb.UsersController, :all
end
```

> **IMPORTANT:** The Approov token binding check needs to come always after the Approov token check itself, because it needs to use the claims from the decoded Approov token payload.

> **NOTE:** When using the Authorization token as the token binding you cannot use the `:approov_token_binding` pipeline for the Authorization API endpoints, as per the above example.

### Approov Token Check for Websockets Requests

For using Websockets in Elixir we have the excellent [Phoenix Channels](https://hexdocs.pm/phoenix/channels.html) abstraction provided by the Phoenix framework, that we can easily secure with Approov.

First, open the file where you establish the websocket connection, maybe `lib/your-app/user_socket.ex` or whatever file name you use, and add to it this code:

```elixir
defp _authorize(socket, params, connect_info) do

  headers = Map.merge(params, connect_info)

  # Always perform the Approov token check before the User Authentication.
  with {:ok, approov_token_claims} <- ApproovToken.verify(headers, _approov_jwk()),
       :ok <- ApproovToken.verify_token_binding(approov_token_claims, params),
       {:ok, current_user} <- Echo.User.authorize(params: params) do

    socket = Phoenix.Socket.assign(socket, context: %{current_user: current_user})

    {:ok, socket}
  else
    {:error, reason} ->
      # log here the error as you see fit.
      :error
  end
end

defp _approov_jwk() do
  %{
    "kty" => "oct",
    "k" =>  Utils.fetch_from_env!(:echo, ApproovToken, :secret_key, 64, :string)
  }
end
```

The above `authorize/3` function needs to be the first one to be invoked in your `connect/3` function pipeline. For example:

```elixir
def connect(params, socket, _connect_info) do
  socket
  |> _authorize(params, connect_info)
  |> _other_stuff()
end
```

This will ensure that the websocket connection is only established when a valid Approov token is provided with a valid token binding, otherwise it's rejected.

Next, you need to secure each event coming through the websocket via a Phoenix Channel, and for that you just need to add to your Phoenix Channel module the following code:

```elixir
defp _authorized(action, payload, socket) do
  Logger.info(%{phoenix_channel_action: action})
  Logger.info(%{phoenix_channel_payload: payload})

  # Always perform the Approov token check before the User Authentication.
  with {:ok, approov_token_claims} <- ApproovToken.verify(payload, _approov_jwk()),
       :ok <- ApproovToken.verify_token_binding(approov_token_claims, payload),
       {:ok, current_user} <- Echo.User.authorize(params: payload),
       true <- Echo.User.can_do_action?(action, current_user) do
    {:ok, socket}
  else
    {:error, reason} ->
      _log_error(reason)
      :error

    false ->
      _log_error("User cannot perform the action: #{action}")
      :error
  end
end

defp _approov_jwk() do
  %{
    "kty" => "oct",
    "k" =>  Utils.fetch_from_env!(:echo, ApproovToken, :secret_key, 64, :string)
  }
end
```

Now, in the `join/3` function and in each `handle_in/3` function of your Phoenix Channel module you need to wrap all you logic in a call to `_authorized/3`:

```elixir
def join("channel:event", payload, socket) do
  case _authorized(:join, payload, socket) do
    {:ok, socket} ->

      # YOUR LOGIC GOES HERE...

      {:ok, socket}

    _ ->
      {:error, %{reason: "Whoops, something went wrong!"}}
  end
end

def handle_in("event:action", payload, socket) do
  case _authorized(:action_name, payload, socket) do
    {:ok, socket} ->

      # YOUR LOGIC GOES HERE...

      {:noreply, socket}

    _ ->
      {:noreply, socket}
  end
end
```

A full working example for a simple Elixir Phoenix Channels server can be found at [src/approov-protected-server/token-binding-check/echo](/src/approov-protected-server/token-binding-check/echo).

[TOC](#toc---table-of-contents)


## Test the Approov Integration

In this section you will see how you can use the Approov CLI to generate sample Approov tokens to test your Approov Integration in the backend without relying in the Mobile App(s) that the backend will be serving.

### Test your Approov Integration with HTTP Requests

The following examples below uses cURL to test the user authentication endpoints, where you need to adjust `auth/register` and `auth/login` to the ones you use. The process of adding the Approov token remains the same for any type of http request, therefore if you don't have user authentication endpoints you can still use the below examples as a reference to test your endpoints.

#### User Registration and Login

Generate a valid token example from the Approov Cloud service:

```bash
approov token -genExample your.api.domain.com
```

Let's try to register the user with the Approov protected endpoint:

```bash
curl -i --request POST 'https://your.api.domain.com/auth/register' \
  --header 'X-Approov-Token: APPROOV_VALID_TOKEN_EXAMPLE_HERE' \
  --data username=test@mail.com \
  --data password=your-pass-here
```

The request should be accepted. For example:

```text
HTTP/2 200
...

{"id":"B4C20F1BBC39F610CF4608F83A06DEA85884D77C854BBB3D10EF239641B9B861"}
```

Next, let's login the user with the Approov protected endpoint:

```bash
curl -i --request POST 'your.api.domain.com/auth/login' \
  --header 'X-Approov-Token: APPROOV_VALID_TOKEN_EXAMPLE_HERE' \
  --data username=test@mail.com \
  --data password=your-pass-here
```

The request should be accepted. For example:

```text
HTTP/2 200
...

{"token":"Bearer AUTHORIZATION_VALID_TOKEN"}
```

Finally we have the Bearer Authorization token that is required to represent a logged-in user when doing queries to the backend. To note that the backend should only check the Authorization token after it is able to successfully check the Approov token.


#### With Valid Approov Tokens

Generate a valid Approov token binding example from the Approov Cloud service:

```bash
approov token -setDataHashInToken 'Bearer AUTHORIZATION_VALID_TOKEN' -genExample your.api.domain.com
```

Then make the request with the generated token:

```bash
curl -i --request GET 'your.api.domain.com/users' \
  --header 'X-Approov-Token: APPROOV_VALID_TOKEN_EXAMPLE_HERE' \
  --header 'Authorization: Bearer AUTHORIZATION_VALID_TOKEN' \
  --header "Content-Type: application/json"
```

The request should be accepted. For example:

```text
HTTP/2 200
...

{
  [
    "id":"B4C20F1BBC39F610CF4608F83A06DEA85884D77C854BBB3D10EF239641B9B861"
  ]
}
```

#### With Invalid Approov Token

Generate an invalid token example from the Approov Cloud service:

```bash
approov token -type invalid -genExample your.api.domain.com
```

Then make the request with the generated token:

```bash
curl -i --request GET 'your.api.domain.com/users' \
  --header 'X-Approov-Token: APPROOV_INVALID_TOKEN_EXAMPLE_HERE' \
  --header 'Authorization: Bearer AUTHORIZATION_VALID_TOKEN' \
  --header "Content-Type: application/json"
```

The above request should fail with an Unauthorized error. For example:

```text
HTTP/2 401

...

{}
```

Despite the Authorization token being a valid one, the server refuses to accept the request, because its presented with an invalid Approov token. We always recommend for the Approov token to be checked before the Authorization token.

[TOC](#toc---table-of-contents)


#### With Invalid Approov Token Binding

Generate a valid Approov token binding example from the Approov Cloud service:

```bash
approov token -setDataHashInToken 'Bearer AUTHORIZATION_VALID_TOKEN' -genExample your.api.domain.com
```

Then make the request with the generated token, but set the Authorization header for a different value from the one you used for `-setDataHashInToken 'Bearer AUTHORIZATION_VALID_TOKEN'`:

```bash
curl -i --request GET 'your.api.domain.com/users' \
  --header 'X-Approov-Token: APPROOV_VALID_TOKEN_BINDING_EXAMPLE_HERE' \
  --header 'Authorization: Bearer AUTHORIZATION_ANOTHER_VALID_TOKEN' \
  --header "Content-Type: application/json"
```

The above request should fail with an Unauthorized error. For example:

```text
HTTP/2 401

...

{}
```

The Authorization header in this request is not the same used for the Approov token binding, thus the backend fails to validate the token binding, despite the Approov token itself being correctly signed and not expired.

Go ahead and try to repeat the request, but this time do not send the Authorization header.


[TOC](#toc---table-of-contents)


### Test your Approov Integration with Websockets

Phoenix Channels cannot be tested using cURL, because you need to establish a websocket connection and keep it open, therefore you will need to use Postman or another software that supports websockets.

We recommend the use of Postman and we provide [this collection](https://raw.githubusercontent.com/approov/postman-collections/master/quickstarts/echo/echo-websockets.postman_collection.json) to help you get started.

You can check [this examples](./../src/approov-protected-server/token-binding-check/echo/README.md#testing-with-postman) to see a practical example of this collection being used. Remember that when following this Postman examples you need to generate the Approov tokens with the Approov CLI, or use the same Approov secret from the examples.

To generate a valid Approov token biding from the Approov Cloud service:

```bash
approov token -setDataHashInToken 'Bearer AUTHORIZATION_VALID_TOKEN' -genExample your.api.domain.com
```

To generate an invalid Approov token example from the Approov Cloud service:

```bash
approov token -setDataHashInToken 'abcdefgh' -genExample your.api.domain.com
```
> The use of `abcdefgh` as value for `-setDataHashInoken` is to simulate the use of a different Authorization token from the one used in the request.

Remember, that for testing for a failed Approov token binding check in the backend you may remove the `Authorization` header/parameter when making the request.

[TOC](#toc---table-of-contents)
